import Dagger: ChunkView, Chunk
using LinearAlgebra, Graphs

@testset "Memory Aliasing" begin
    A = rand(4)
    a = Dagger.aliasing(A)
    @test a isa Dagger.ContiguousAliasing
    @test a.span.ptr.addr == UInt(pointer(A))
    @test a.span.len == sizeof(Float64) * length(A)

    r = Ref(3)
    a = Dagger.aliasing(r)
    @test a isa Dagger.CombinedAliasing
    @test length(a.sub_ainfos) == 1
    s = only(a.sub_ainfos)
    @test s isa Dagger.ObjectAliasing
    @test s.ptr == pointer_from_objref(r)
    @test s.sz == sizeof(3)
end

@testset "ChunkView" begin
    DA = rand(Blocks(8, 8), 64, 64)
    task1 = DA.chunks[1,1]::DTask
    chunk1 = fetch(task1; raw=true)::Chunk
    v1 = view(chunk1, :, :)
    task2 = DA.chunks[1,2]::DTask
    chunk2 = fetch(task2; raw=true)::Chunk
    v2 = view(chunk2, :, :)

    for obj in (chunk1, task1)
        @testset "Valid Slices" begin
            @test view(obj, :, :)     isa ChunkView && view(obj, 1:8, 1:8)   isa ChunkView
            @test view(obj, 1:2:7, :) isa ChunkView && view(obj, :, 2:2:8)   isa ChunkView
            @test view(obj, 1, :)     isa ChunkView && view(obj, :, 1)       isa ChunkView
            @test view(obj, 3:3, 5:5) isa ChunkView && view(obj, 5:7, 1:2:4) isa ChunkView
            @test view(obj, 8, 8)     isa ChunkView
            @test view(obj, 1:0, :)   isa ChunkView
        end

        @testset "Dimension Mismatch" begin
            @test_throws DimensionMismatch view(obj, :)
            @test_throws DimensionMismatch view(obj, :, :, :)
        end

        @testset "Int Slice Out of Bounds" begin
            @test_throws ArgumentError view(obj, 0, :)
            @test_throws ArgumentError view(obj, :, 9)
            @test_throws ArgumentError view(obj, 9, 1)
        end

        @testset "Range Slice Out of Bounds" begin
            @test_throws ArgumentError view(obj, 0:5, :)
            @test_throws ArgumentError view(obj, 1:8, 5:10)
            @test_throws ArgumentError view(obj, 2:2:10, :)
        end

        @testset "Invalid Slice Types" begin
            @test_throws DimensionMismatch view(obj, (1:2, :))
            @test_throws ArgumentError view(obj, :, [1, 2])
        end
    end

    @test fetch(v1) == fetch(chunk1)

    @test Dagger.memory_space(v1) == Dagger.memory_space(chunk1)
    @test Dagger.aliasing(v1) isa Dagger.StridedAliasing
    ptr = remotecall_fetch(chunk1.handle.owner, chunk1) do chunk
        UInt(pointer(Dagger.unwrap(chunk)))
    end
    @test Dagger.aliasing(v1).base_ptr.addr == ptr

    @testset "Aliasing" begin
        f! = v1 -> begin
            v1 .= 0
            return
        end
        Dagger.spawn_datadeps() do
            Dagger.@spawn f!(InOut(v1))
        end
        @test collect(DA)[1:8, 1:8] == zeros(8, 8)
    end
end

function with_logs(f)
    Dagger.enable_logging!(;taskdeps=true, taskargs=true)
    try
        f()
        return Dagger.fetch_logs!()
    finally
        Dagger.disable_logging!()
    end
end

@testset "Recursive move!" begin
    # Define some structs for testing
    mutable struct SimpleMutable
        a::Int
        b::String
    end

    struct SimpleImmutable
        a::Int
        b::String
    end

    mutable struct NestedMutable
        x::Int
        sm::SimpleMutable
        arr::Vector{Float64}
        imm::SimpleImmutable
    end

    struct NestedImmutable
        x::Int
        sm::SimpleMutable # Mutable field within immutable struct
        arr::Vector{Float64} # Mutable field
        imm::SimpleImmutable # Immutable field
        ptr_free_imm::SimpleImmutable # For pointer-free check
    end

    # Helper to check equality of fields, useful for structs
    function check_fields_equal(s1, s2)
        typeof(s1) != typeof(s2) && return false
        for fname in fieldnames(typeof(s1))
            v1 = getfield(s1, fname)
            v2 = getfield(s2, fname)
            if v1 isa AbstractArray && v2 isa AbstractArray
                if !(v1 == v2) return false end
            elseif isstructtype(typeof(v1))
                if !check_fields_equal(v1, v2) return false end
            else
                if !(v1 == v2) return false end
            end
        end
        return true
    end

    @testset "Simple Types and Structs" begin
        # Mutable struct with simple fields
        sm1_from = SimpleMutable(1, "hello")
        sm1_to = SimpleMutable(0, "")
        Dagger.move!(sm1_to, sm1_from)
        @test sm1_to.a == 1
        @test sm1_to.b == "hello"

        # Test with identical objects (should be a no-op)
        sm_identical = SimpleMutable(10, "identical")
        Dagger.move!(sm_identical, sm_identical) # Should not recurse infinitely
        @test sm_identical.a == 10
        @test sm_identical.b == "identical"
    end

    @testset "AbstractArray Types" begin
        arr_from = [1.0, 2.0, 3.0]
        arr_to = [0.0, 0.0, 0.0]
        # move! is for structs; for raw arrays, copyto! is the direct equivalent
        # However, if an array is a field of a struct, it should be handled.

        mutable struct StructWithArray
            id::Int
            data::Vector{Float64}
        end

        swa_from = StructWithArray(1, [10.0, 20.0])
        swa_to = StructWithArray(0, [0.0, 0.0])
        Dagger.move!(swa_to, swa_from)
        @test swa_to.id == 1
        @test swa_to.data == [10.0, 20.0]
        @test swa_to.data !== swa_from.data # copyto! should copy contents, not ref

        # Test array size mismatch (should warn and skip)
        swa_from_diff_size = StructWithArray(2, [1.0, 2.0, 3.0])
        swa_to_small_arr = StructWithArray(0, [0.0])
        # Expect a warning, swa_to_small_arr.data should remain unchanged
        #@test_logs (:warn, r"Field 'data': Array types/sizes mismatch") Dagger.move!(swa_to_small_arr, swa_from_diff_size)
        # For now, manually check state as @test_logs can be tricky with Dagger's output
        Dagger.move!(swa_to_small_arr, swa_from_diff_size) # This will log a warning
        @test swa_to_small_arr.id == 2 # ID should be copied
        @test swa_to_small_arr.data == [0.0] # Array data should not be copied due to size mismatch
    end

    @testset "Nested Structs" begin
        nm_from = NestedMutable(
            10,
            SimpleMutable(100, "nested_from"),
            [1.1, 2.2],
            SimpleImmutable(1000, "immutable_from")
        )
        nm_to = NestedMutable(
            0,
            SimpleMutable(0, ""),
            [0.0, 0.0],
            SimpleImmutable(0, "")
        )
        Dagger.move!(nm_to, nm_from)
        @test nm_to.x == 10
        @test nm_to.sm.a == 100
        @test nm_to.sm.b == "nested_from"
        @test nm_to.arr == [1.1, 2.2]
        # For immutable fields of mutable structs, setfield! replaces the instance
        @test nm_to.imm.a == 1000
        @test nm_to.imm.b == "immutable_from"
        # After `setfield!(nm_to, :imm, nm_from.imm)`, they should be the same instance.
        @test nm_to.imm === nm_from.imm

        # Check that original mutable objects within `from` are not the same instances in `to` if they were copied/recreated
        @test nm_to.sm !== nm_from.sm # SimpleMutable is mutable, so `move!` recurses, fields are set
        @test nm_to.arr !== nm_from.arr # Vector is copied by copyto!
    end

    @testset "Immutable Structs and Skip Condition" begin
        # Case: Surrounding struct is immutable, field is immutable, pointer-free -> skip
        ni_from = NestedImmutable(
            1,
            SimpleMutable(10, "mutable_field_from"), # this field is mutable
            [1.0, 2.0], # this field is mutable (Vector)
            SimpleImmutable(100, "immutable_field_from"), # this field is immutable
            SimpleImmutable(200, "ptr_free_val_from") # this field is immutable and pointer-free
        )
        ni_to = NestedImmutable(
            0,
            SimpleMutable(0, "original_to_mutable"),
            [0.0, 0.0],
            SimpleImmutable(0, "original_to_immutable"),
            SimpleImmutable(0, "original_to_ptr_free")
        )

        # Base.isstructtype(typeof(ni_to.ptr_free_imm)) -> true
        # !ismutable(ni_to.ptr_free_imm) -> true (SimpleImmutable is immutable)
        # Base.datatype_pointerfree(typeof(ni_to.ptr_free_imm)) -> true (SimpleImmutable is pointer-free)
        # !ismutable(typeof(ni_to)) -> true (NestedImmutable is immutable)

        Dagger.move!(ni_to, ni_from)

        # For an immutable `ni_to`, `move!` cannot change its direct fields using `setfield!`.
        # It can only affect `ni_to` if its fields are mutable objects whose contents are changed.

        # ni_to.x is Int, part of immutable struct, cannot change.
        @test ni_to.x == 0

        # ni_to.sm is SimpleMutable. `move!` will be called on ni_to.sm and ni_from.sm.
        # Since SimpleMutable is mutable, its fields will be updated.
        @test ni_to.sm.a == 10
        @test ni_to.sm.b == "mutable_field_from"
        @test ni_to.sm !== ni_from.sm # They are distinct objects, but ni_to.sm was mutated.

        # ni_to.arr is Vector{Float64}. `copyto!` will be used.
        @test ni_to.arr == [1.0, 2.0]
        @test ni_to.arr !== ni_from.arr # ni_to.arr was mutated by copyto!.

        # ni_to.imm is SimpleImmutable. It's an immutable field of an immutable struct.
        # The skip condition: !ismutable(NestedImmutable) && !ismutable(SimpleImmutable) && Base.datatype_pointerfree(SimpleImmutable)
        # This is true. So this field should be skipped.
        # Therefore, ni_to.imm should remain "original_to_immutable".
        # However, the current implementation of `move!` has an initial check `!ismutable(to) return to`.
        # This means for `move!(ni_to, ni_from)`, it returns `ni_to` immediately if `NestedImmutable` is immutable.
        # This needs to be reconciled with the per-field skip logic.
        # The prompt implies `move!` should still recurse into mutable fields of immutable structs.
        # Let's adjust the initial check of `move!` or assume the version I wrote handles this.
        # The last version of `move!` I wrote:
        # `!isstructtype(T) && return to # Or throw error`
        # This allows `move!` to proceed for immutable structs.
        # Then, for a field like `x::Int` in an immutable struct, `setfield!` is skipped because parent `T` is immutable.
        # For a field `imm::SimpleImmutable`, if it's pointer-free, it's skipped.

        # With the refined `move!`:
        # - ni_to.x: Int. Parent NestedImmutable is immutable. `setfield!` won't be called. Remains 0.
        @test ni_to.x == 0

        # - ni_to.sm: SimpleMutable. `move!(ni_to.sm, ni_from.sm)` is called. ni_to.sm is mutated.
        @test ni_to.sm.a == 10
        @test ni_to.sm.b == "mutable_field_from"

        # - ni_to.arr: Vector. `copyto!(ni_to.arr, ni_from.arr)` is called. ni_to.arr is mutated.
        @test ni_to.arr == [1.0, 2.0]

        # - ni_to.imm: SimpleImmutable.
        #   Parent NestedImmutable is immutable. Field SimpleImmutable is immutable.
        #   `Base.datatype_pointerfree(SimpleImmutable)` is true.
        #   So, this field `imm` should be skipped by the condition.
        @test ni_to.imm.a == 0 # Should be unchanged from original_to_immutable
        @test ni_to.imm.b == "original_to_immutable"

        # - ni_to.ptr_free_imm: SimpleImmutable. Same logic as ni_to.imm. Should be skipped.
        @test ni_to.ptr_free_imm.a == 0 # Should be unchanged from original_to_ptr_free
        @test ni_to.ptr_free_imm.b == "original_to_ptr_free"
    end

    @testset "LU Factorization Objects" begin
        using LinearAlgebra # Ensure it's explicitly used here, though already in outer scope
        println("Julia version in LU test: ", VERSION) # Print version

        A_from_data = rand(Float64, 3, 3)
        # Ensure A_from_data is not singular for robust testing if needed, though LU handles singular.
        # For simplicity, assume rand() is usually non-singular for small matrices.
        A_to_data = zeros(Float64, 3, 3) # Ensure different initial data

        # Make them DMatrix like, but just regular matrices for this local test for simplicity,
        # as Dagger.move! is generic. The LU object structure is what matters.
        # If LU contained DMatrix, then Dagger.move! on DMatrix fields would be tested by array part.

        # Create LU objects
        # Ensure A_from_data is factorizable without pivots for simplicity if NoPivot is used
        # For pivoted LU, ipiv is also important.
        # The LU struct from Base is:
        # struct LU{T,S<:AbstractMatrix{T},P<:AbstractVector{BlasInt}} <: Factorization{T}
        #     factors::S
        #     ipiv::P
        #     info::BlasInt
        # end
        # This struct is immutable. Its fields (factors, ipiv) are arrays (mutable).

        F_from = lu(A_from_data) # Uses pivoted LU by default

        # Use a different, but valid, non-singular matrix for F_to's initial state
        A_to_init_data = Matrix{Float64}(I, size(A_from_data)) * 2.0 # A different non-singular matrix
        A_to_init_data[1,2] = 0.5 # Make it a bit different from scaled Identity
        F_to = lu(A_to_init_data) # Target LU object, now from a valid factorization

        # Store original F_to.info because it should not change
        original_F_to_info = F_to.info
        @test original_F_to_info == 0 # Should be 0 for a successful LU on A_to_init_data

        # Check initial state of F_to just to be sure it's different
        @test F_to.factors != F_from.factors
        # ipiv might be the same if the structure of pivots happens to align, so not a strong test here.
        # @test F_to.ipiv != F_from.ipiv

        # Perform the move
        # Since LU is immutable, move!(F_to, F_from) will not change F_to itself.
        # It will operate on its mutable fields: F_to.factors and F_to.ipiv.
        Dagger.move!(F_to, F_from)

        # Verify that F_to's internal data now matches F_from's for mutable fields
        @test F_to.factors == F_from.factors
        @test F_to.factors !== F_from.factors # copyto! ensures different underlying array objects for factors

        @test F_to.ipiv == F_from.ipiv
        @test F_to.ipiv !== F_from.ipiv # copyto! for ipiv

        # The `info` field is a BlasInt (primitive-like) within an immutable LU struct.
        # According to the rules:
        # - Parent `LU` is immutable.
        # - Field `info` (BlasInt) is immutable.
        # - `Base.datatype_pointerfree(typeof(F_to.info))` is true.
        # So, this field should be SKIPPED by the `move!` logic.
        # Thus, F_to.info should retain its original value.
        @test F_to.info == original_F_to_info
        @test F_to.info == F_from.info # This will pass if both factorizations were successful (info=0)

        # More detailed check: reconstruct matrix from F_to and see if it matches A_from_data
        # L*U should give P*A_from_data
        L_to = LowerTriangular(F_to.factors)
        for i in 1:size(L_to,1); L_to[i,i] = 1.0; end
        U_to = UpperTriangular(F_to.factors)
        # Use LinearAlgebra.ipiv_to_perm to correctly convert ipiv to a permutation vector
        # Try calling via getfield to diagnose UndefVarError
        ipiv_to_perm_func = getfield(LinearAlgebra, :ipiv_to_perm)
        perm_vec = ipiv_to_perm_func(F_to.ipiv, size(A_from_data,1))
        P_to_matrix = Matrix{Float64}(I, size(A_from_data))[:, perm_vec] # Permutation matrix

        # Reconstructed A from F_to should be P_from_inv * L_from * U_from
        # A = P⁻¹LU. So P*A = LU.
        # The factors store L and U combined.
        # If we apply F_to to a vector, or solve a system, it should behave like F_from.
        b = rand(Float64, 3)
        x_from = F_from \ b
        x_to = F_to \ b
        @test x_to ≈ x_from

        # Test with LU{T, DMatrix{T}, DVector{Int}} if Dagger has such types and they are integrated
        # For now, this tests the general recursive logic with Base.LU
    end
end
task_id(t::Dagger.DTask) = lock(Dagger.Sch.EAGER_ID_MAP) do id_map
    id_map[t.uid]
end
function taskdeps_for_task(logs::Dict{Int,<:Dict}, tid::Int)
    for w in keys(logs)
        _logs = logs[w]
        for idx in 1:length(_logs[:core])
            core_log = _logs[:core][idx]
            if core_log.category == :add_thunk && core_log.kind == :start
                taskdeps = _logs[:taskdeps][idx]::Pair{Int,Vector{Int}}
                if taskdeps[1] == tid
                    return taskdeps[2]
                end
            end
        end
    end
    error("Task $tid not found in logs")
end
function test_task_dominators(logs::Dict, tid::Int, doms::Vector; all_tids::Vector=[], nondom_check::Bool=false)
    g = SimpleDiGraph()
    tid_to_v = Dict{Int,Int}()
    seen = Set{Int}()
    to_visit = copy(all_tids)
    while !isempty(to_visit)
        this_tid = popfirst!(to_visit)
        this_tid in seen && continue
        push!(seen, this_tid)
        if !(this_tid in keys(tid_to_v))
            add_vertex!(g); tid_to_v[this_tid] = nv(g)
        end

        # Add syncdeps
        deps = taskdeps_for_task(logs, this_tid)
        for dep in deps
            if !(dep in keys(tid_to_v))
                add_vertex!(g); tid_to_v[dep] = nv(g)
            end
            add_edge!(g, tid_to_v[this_tid], tid_to_v[dep])
            push!(to_visit, dep)
        end
    end
    state = dijkstra_shortest_paths(g, tid_to_v[tid])
    any_failed = false
    @test !has_edge(g, tid_to_v[tid], tid_to_v[tid])
    any_failed |= has_edge(g, tid_to_v[tid], tid_to_v[tid])
    for dom in doms
        @test state.pathcounts[tid_to_v[dom]] > 0
        if state.pathcounts[tid_to_v[dom]] == 0
            println("Expected dominance for $dom of $tid")
            any_failed = true
        end
    end
    if nondom_check
        for nondom in all_tids
            nondom == tid && continue
            nondom in doms && continue
            @test state.pathcounts[tid_to_v[nondom]] == 0
            if state.pathcounts[tid_to_v[nondom]] > 0
                println("Expected non-dominance for $nondom of $tid")
                any_failed = true
            end
        end
    end

    # For debugging purposes
    if any_failed
        println("Failure detected!")
        println("Root: $tid")
        println("Exp. doms: $doms")
        println("All: $all_tids")
        e_vs = collect(edges(g))
        e_tids = map(e->Edge(only(filter(tv->tv[2]==src(e), tid_to_v))[1],
                             only(filter(tv->tv[2]==dst(e), tid_to_v))[1]),
                     e_vs)
        sort!(e_tids)
        for e in e_tids
            s_tid, d_tid = src(e), dst(e)
            println("Edge: $s_tid -(up)> $d_tid")
        end
    end
end

@everywhere do_nothing(Xs...) = nothing
@everywhere mut_ref!(R) = (R[] .= 0;)
@everywhere mut_V!(V) = (V .= 1;)
function test_datadeps(;args_chunks::Bool,
                        args_thunks::Bool,
                        args_loc::Int,
                        aliasing::Bool)
    # Returns last value
    @test Dagger.spawn_datadeps(;aliasing) do
        42
    end == 42

    # Tasks are started and finished as spawn_datadeps returns
    ts = []
    Dagger.spawn_datadeps(;aliasing) do
        for i in 1:5
            t = Dagger.@spawn sleep(0.1)
            @test !istaskstarted(t)
        end
    end
    @test all(istaskdone, ts)

    # Rethrows any task exceptions
    @test_throws Exception Dagger.spawn_datadeps(;aliasing) do
        Dagger.@spawn error("Test")
    end

    A = rand(1)
    if args_chunks
        A = remotecall_fetch(Dagger.tochunk, args_loc, A)
    elseif args_thunks
        A = Dagger.@spawn scope=Dagger.scope(worker=args_loc) copy(A)
    end

    # Task return values can be tracked
    ts = []
    logs = with_logs() do
        Dagger.spawn_datadeps(;aliasing) do
            t1 = Dagger.@spawn fill(42, 1)
            push!(ts, t1)
            push!(ts, Dagger.@spawn copyto!(Out(A), In(t1)))
        end
    end
    tid_1, tid_2 = task_id.(ts)
    @test fetch(A)[1] == 42.0
    test_task_dominators(logs, tid_1, []; all_tids=[tid_1, tid_2])
    # FIXME: We don't record the task as a syncdep, but instead internally `fetch` the chunk
    test_task_dominators(logs, tid_2, [#=tid_1=#]; all_tids=[tid_1, tid_2])

    # R->R Non-Aliasing
    ts = []
    logs = with_logs() do
        Dagger.spawn_datadeps(;aliasing) do
            push!(ts, Dagger.@spawn do_nothing(In(A)))
            push!(ts, Dagger.@spawn do_nothing(In(A)))
        end
    end
    tid_1, tid_2 = task_id.(ts)
    test_task_dominators(logs, tid_1, []; all_tids=[tid_1, tid_2])
    test_task_dominators(logs, tid_2, []; all_tids=[tid_1, tid_2], nondom_check=false)

    # R->W Aliasing
    ts = []
    logs = with_logs() do
        Dagger.spawn_datadeps(;aliasing) do
            push!(ts, Dagger.@spawn do_nothing(In(A)))
            push!(ts, Dagger.@spawn do_nothing(Out(A)))
        end
    end
    tid_1, tid_2 = task_id.(ts)
    test_task_dominators(logs, tid_1, []; all_tids=[tid_1, tid_2])
    test_task_dominators(logs, tid_2, [tid_1]; all_tids=[tid_1, tid_2])

    # W->W Aliasing
    ts = []
    logs = with_logs() do
        Dagger.spawn_datadeps(;aliasing) do
            push!(ts, Dagger.@spawn do_nothing(Out(A)))
            push!(ts, Dagger.@spawn do_nothing(Out(A)))
        end
    end
    tid_1, tid_2 = task_id.(ts)
    test_task_dominators(logs, tid_1, []; all_tids=[tid_1, tid_2])
    test_task_dominators(logs, tid_2, [tid_1]; all_tids=[tid_1, tid_2])

    # R->R Non-Self-Aliasing
    ts = []
    logs = with_logs() do
        Dagger.spawn_datadeps(;aliasing) do
            push!(ts, Dagger.@spawn do_nothing(In(A), In(A)))
            push!(ts, Dagger.@spawn do_nothing(In(A), In(A)))
        end
    end
    tid_1, tid_2 = task_id.(ts)
    test_task_dominators(logs, tid_1, []; all_tids=[tid_1, tid_2])
    test_task_dominators(logs, tid_2, []; all_tids=[tid_1, tid_2])

    # R->W Self-Aliasing
    ts = []
    logs = with_logs() do
        Dagger.spawn_datadeps(;aliasing) do
            push!(ts, Dagger.@spawn do_nothing(In(A), In(A)))
            push!(ts, Dagger.@spawn do_nothing(Out(A), Out(A)))
        end
    end
    tid_1, tid_2 = task_id.(ts)
    test_task_dominators(logs, tid_1, []; all_tids=[tid_1, tid_2])
    test_task_dominators(logs, tid_2, [tid_1]; all_tids=[tid_1, tid_2])

    # W->W Self-Aliasing
    ts = []
    logs = with_logs() do
        Dagger.spawn_datadeps(;aliasing) do
            push!(ts, Dagger.@spawn do_nothing(Out(A), Out(A)))
            push!(ts, Dagger.@spawn do_nothing(Out(A), Out(A)))
        end
    end
    tid_1, tid_2 = task_id.(ts)
    test_task_dominators(logs, tid_1, []; all_tids=[tid_1, tid_2])
    test_task_dominators(logs, tid_2, [tid_1]; all_tids=[tid_1, tid_2])

    if aliasing
        function wrap_chunk_thunk(f, args...)
            if args_thunks || args_chunks
                result = Dagger.@spawn scope=Dagger.scope(worker=args_loc) f(args...)
                if args_thunks
                    return result
                elseif args_chunks
                    return fetch(result; raw=true)
                end
            else
                # N.B. We don't allocate remotely for raw data
                return f(args...)
            end
        end
        B = wrap_chunk_thunk(rand, 4, 4)

        # Views
        B_ul = wrap_chunk_thunk(view, B, 1:2, 1:2)
        B_ur = wrap_chunk_thunk(view, B, 1:2, 3:4)
        B_ll = wrap_chunk_thunk(view, B, 3:4, 1:2)
        B_lr = wrap_chunk_thunk(view, B, 3:4, 3:4)
        B_mid = wrap_chunk_thunk(view, B, 2:3, 2:3)
        for (B_name, B_view) in (
                                 (:B_ul, B_ul),
                                 (:B_ur, B_ur),
                                 (:B_ll, B_ll),
                                 (:B_lr, B_lr),
                                 (:B_mid, B_mid))
            @test Dagger.will_alias(Dagger.aliasing(B), Dagger.aliasing(B_view))
            B_view === B_mid && continue
            @test Dagger.will_alias(Dagger.aliasing(B_mid), Dagger.aliasing(B_view))
        end
        local t_A, t_B, t_ul, t_ur, t_ll, t_lr, t_mid
        local t_ul2, t_ur2, t_ll2, t_lr2
        logs = with_logs() do
            Dagger.spawn_datadeps(;aliasing) do
                t_A = Dagger.@spawn do_nothing(InOut(A))
                t_B = Dagger.@spawn do_nothing(InOut(B))
                t_ul = Dagger.@spawn do_nothing(InOut(B_ul))
                t_ur = Dagger.@spawn do_nothing(InOut(B_ur))
                t_ll = Dagger.@spawn do_nothing(InOut(B_ll))
                t_lr = Dagger.@spawn do_nothing(InOut(B_lr))
                t_mid = Dagger.@spawn do_nothing(InOut(B_mid))
                t_ul2 = Dagger.@spawn do_nothing(InOut(B_ul))
                t_ur2 = Dagger.@spawn do_nothing(InOut(B_ur))
                t_ll2 = Dagger.@spawn do_nothing(InOut(B_ll))
                t_lr2 = Dagger.@spawn do_nothing(InOut(B_lr))
            end
        end
        tid_A, tid_B, tid_ul, tid_ur, tid_ll, tid_lr, tid_mid =
            task_id.([t_A, t_B, t_ul, t_ur, t_ll, t_lr, t_mid])
        tid_ul2, tid_ur2, tid_ll2, tid_lr2 =
            task_id.([t_ul2, t_ur2, t_ll2, t_lr2])
        tids_all = [tid_A, tid_B, tid_ul, tid_ur, tid_ll, tid_lr, tid_mid,
                    tid_ul2, tid_ur2, tid_ll2, tid_lr2]
        test_task_dominators(logs, tid_A, []; all_tids=tids_all)
        test_task_dominators(logs, tid_B, []; all_tids=tids_all)
        test_task_dominators(logs, tid_ul, [tid_B]; all_tids=tids_all)
        test_task_dominators(logs, tid_ur, [tid_B]; all_tids=tids_all)
        test_task_dominators(logs, tid_ll, [tid_B]; all_tids=tids_all)
        test_task_dominators(logs, tid_lr, [tid_B]; all_tids=tids_all)
        test_task_dominators(logs, tid_mid, [tid_B, tid_ul, tid_ur, tid_ll, tid_lr]; all_tids=tids_all)
        test_task_dominators(logs, tid_ul2, [tid_B, tid_mid, tid_ul]; all_tids=tids_all, nondom_check=false)
        test_task_dominators(logs, tid_ur2, [tid_B, tid_mid, tid_ur]; all_tids=tids_all, nondom_check=false)
        test_task_dominators(logs, tid_ll2, [tid_B, tid_mid, tid_ll]; all_tids=tids_all, nondom_check=false)
        test_task_dominators(logs, tid_lr2, [tid_B, tid_mid, tid_lr]; all_tids=tids_all, nondom_check=false)

        # (Unit)Upper/LowerTriangular and Diagonal
        B_upper = wrap_chunk_thunk(UpperTriangular, B)
        B_unitupper = wrap_chunk_thunk(UnitUpperTriangular, B)
        B_lower = wrap_chunk_thunk(LowerTriangular, B)
        B_unitlower = wrap_chunk_thunk(UnitLowerTriangular, B)
        for (B_name, B_view) in (
                                 (:B_upper, B_upper),
                                 (:B_unitupper, B_unitupper),
                                 (:B_lower, B_lower),
                                 (:B_unitlower, B_unitlower))
            @test Dagger.will_alias(Dagger.aliasing(B), Dagger.aliasing(B_view))
        end
        @test Dagger.will_alias(Dagger.aliasing(B_upper), Dagger.aliasing(B_lower))
        @test !Dagger.will_alias(Dagger.aliasing(B_unitupper), Dagger.aliasing(B_unitlower))
        @test Dagger.will_alias(Dagger.aliasing(B_upper), Dagger.aliasing(B_unitupper))
        @test Dagger.will_alias(Dagger.aliasing(B_lower), Dagger.aliasing(B_unitlower))

        @test Dagger.will_alias(Dagger.aliasing(B_upper), Dagger.aliasing(B, Diagonal))
        @test Dagger.will_alias(Dagger.aliasing(B_lower), Dagger.aliasing(B, Diagonal))
        @test !Dagger.will_alias(Dagger.aliasing(B_unitupper), Dagger.aliasing(B, Diagonal))
        @test !Dagger.will_alias(Dagger.aliasing(B_unitlower), Dagger.aliasing(B, Diagonal))

        local t_A, t_B, t_upper, t_unitupper, t_lower, t_unitlower, t_diag
        local t_upper2, t_unitupper2, t_lower2, t_unitlower2
        logs = with_logs() do
            Dagger.spawn_datadeps(;aliasing) do
                t_A = Dagger.@spawn do_nothing(InOut(A))
                t_B = Dagger.@spawn do_nothing(InOut(B))
                t_upper = Dagger.@spawn do_nothing(InOut(B_upper))
                t_unitupper = Dagger.@spawn do_nothing(InOut(B_unitupper))
                t_lower = Dagger.@spawn do_nothing(InOut(B_lower))
                t_unitlower = Dagger.@spawn do_nothing(InOut(B_unitlower))
                t_diag = Dagger.@spawn do_nothing(Deps(B, InOut(Diagonal)))
                t_unitlower2 = Dagger.@spawn do_nothing(InOut(B_unitlower))
                t_lower2 = Dagger.@spawn do_nothing(InOut(B_lower))
                t_unitupper2 = Dagger.@spawn do_nothing(InOut(B_unitupper))
                t_upper2 = Dagger.@spawn do_nothing(InOut(B_upper))
            end
        end
        tid_A, tid_B, tid_upper, tid_unitupper, tid_lower, tid_unitlower, tid_diag =
            task_id.([t_A, t_B, t_upper, t_unitupper, t_lower, t_unitlower, t_diag])
        tid_upper2, tid_unitupper2, tid_lower2, tid_unitlower2 =
            task_id.([t_upper2, t_unitupper2, t_lower2, t_unitlower2])
        tids_all = [tid_A, tid_B, tid_upper, tid_unitupper, tid_lower, tid_unitlower, tid_diag,
                    tid_upper2, tid_unitupper2, tid_lower2, tid_unitlower2]
        test_task_dominators(logs, tid_A, []; all_tids=tids_all)
        test_task_dominators(logs, tid_B, []; all_tids=tids_all)
        # FIXME: Proper non-dominance checks
        test_task_dominators(logs, tid_upper, [tid_B]; all_tids=tids_all, nondom_check=false)
        test_task_dominators(logs, tid_unitupper, [tid_B, tid_upper]; all_tids=tids_all, nondom_check=false)
        test_task_dominators(logs, tid_lower, [tid_B, tid_upper]; all_tids=tids_all, nondom_check=false)
        test_task_dominators(logs, tid_unitlower, [tid_B, tid_lower]; all_tids=tids_all, nondom_check=false)
        test_task_dominators(logs, tid_diag, [tid_B, tid_upper, tid_lower]; all_tids=tids_all, nondom_check=false)
        test_task_dominators(logs, tid_unitlower2, [tid_B, tid_lower, tid_unitlower]; all_tids=tids_all, nondom_check=false)
        test_task_dominators(logs, tid_lower2, [tid_B, tid_lower, tid_unitlower, tid_diag, tid_unitlower2]; all_tids=tids_all, nondom_check=false)
        test_task_dominators(logs, tid_unitupper2, [tid_B, tid_upper, tid_unitupper]; all_tids=tids_all, nondom_check=false)
        test_task_dominators(logs, tid_upper2, [tid_B, tid_upper, tid_unitupper, tid_diag, tid_unitupper2]; all_tids=tids_all, nondom_check=false)

        # Additional aliasing tests
        views_overlap(x, y) = Dagger.will_alias(Dagger.aliasing(x), Dagger.aliasing(y))

        A = wrap_chunk_thunk(identity, B)

        A_r1 = wrap_chunk_thunk(view, A, 1:1, 1:4)
        A_r2 = wrap_chunk_thunk(view, A, 2:2, 1:4)
        B_r1 = wrap_chunk_thunk(view, B, 1:1, 1:4)
        B_r2 = wrap_chunk_thunk(view, B, 2:2, 1:4)

        A_c1 = wrap_chunk_thunk(view, A, 1:4, 1:1)
        A_c2 = wrap_chunk_thunk(view, A, 1:4, 2:2)
        B_c1 = wrap_chunk_thunk(view, B, 1:4, 1:1)
        B_c2 = wrap_chunk_thunk(view, B, 1:4, 2:2)

        A_mid = wrap_chunk_thunk(view, A, 2:3, 2:3)
        B_mid = wrap_chunk_thunk(view, B, 2:3, 2:3)

        @test views_overlap(A_r1, A_r1)
        @test views_overlap(B_r1, B_r1)
        @test views_overlap(A_c1, A_c1)
        @test views_overlap(B_c1, B_c1)

        @test views_overlap(A_r1, B_r1)
        @test views_overlap(A_r2, B_r2)
        @test views_overlap(A_c1, B_c1)
        @test views_overlap(A_c2, B_c2)

        @test !views_overlap(A_r1, A_r2)
        @test !views_overlap(B_r1, B_r2)
        @test !views_overlap(A_c1, A_c2)
        @test !views_overlap(B_c1, B_c2)

        @test views_overlap(A_r1, A_c1)
        @test views_overlap(A_r1, B_c1)
        @test views_overlap(A_r2, A_c2)
        @test views_overlap(A_r2, B_c2)

        for (name, mid) in ((:A_mid, A_mid), (:B_mid, B_mid))
            @test !views_overlap(A_r1, mid)
            @test !views_overlap(B_r1, mid)
            @test !views_overlap(A_c1, mid)
            @test !views_overlap(B_c1, mid)

            @test views_overlap(A_r2, mid)
            @test views_overlap(B_r2, mid)
            @test views_overlap(A_c2, mid)
            @test views_overlap(B_c2, mid)
        end

        @test views_overlap(A_mid, A_mid)
        @test views_overlap(A_mid, B_mid)

        # SubArray hashing
        V = zeros(3)
        Dagger.spawn_datadeps(;aliasing) do
            Dagger.@spawn mut_V!(InOut(view(V, 1:2)))
            Dagger.@spawn mut_V!(InOut(view(V, 2:3)))
        end
        @test fetch(V) == [1, 1, 1]
    end

    # FIXME: Deps

    # Outer Scope
    exec_procs = fetch.(Dagger.spawn_datadeps(;aliasing) do
        [Dagger.@spawn Dagger.task_processor() for i in 1:10]
    end)
    unique!(exec_procs)
    scope = Dagger.get_compute_scope()
    all_procs = vcat([collect(Dagger.get_processors(OSProc(w))) for w in procs()]...)
    scope_procs = filter(proc->!isa(Dagger.constrain(scope, ExactScope(proc)), Dagger.InvalidScope), all_procs)
    for proc in exec_procs
        @test proc in scope_procs
    end
    for proc in scope_procs
        proc == Dagger.ThreadProc(1, 1) && continue
        @test proc in exec_procs
    end

    # Inner Scope
    @test_throws Dagger.Sch.SchedulingException Dagger.spawn_datadeps(;aliasing) do
        Dagger.@spawn scope=Dagger.ExactScope(Dagger.ThreadProc(1, 5000)) 1+1
    end

    # Field aliasing
    X = Ref(rand(1000))
    @test all(x->x==0, fetch(Dagger.spawn_datadeps() do
        Dagger.@spawn mut_ref!(Deps(X, InOut(:x)))
        Dagger.@spawn getfield(Deps(X, In(:x)), :x)
    end))

    # Add-to-copy
    A = rand(1000)
    B = rand(1000)
    C = rand(1000)
    D = zeros(1000)
    add!(X, Y) = (X .+= Y;)
    expected = (A .+ B) .+ (A .+ C)
    if args_chunks
        A = remotecall_fetch(Dagger.tochunk, args_loc, A)
        B = remotecall_fetch(Dagger.tochunk, args_loc, B)
        C = remotecall_fetch(Dagger.tochunk, args_loc, C)
        D = remotecall_fetch(Dagger.tochunk, args_loc, D)
    elseif args_thunks
        A = Dagger.@spawn scope=Dagger.scope(worker=args_loc) copy(A)
        B = Dagger.@spawn scope=Dagger.scope(worker=args_loc) copy(B)
        C = Dagger.@spawn scope=Dagger.scope(worker=args_loc) copy(C)
        D = Dagger.@spawn scope=Dagger.scope(worker=args_loc) copy(D)
    end
    Dagger.spawn_datadeps(;aliasing) do
        Dagger.@spawn add!(InOut(B), In(A))
        Dagger.@spawn add!(InOut(C), In(A))
        Dagger.@spawn add!(InOut(C), In(B))
        Dagger.@spawn copyto!(Out(D), In(C))
    end
    @test isapprox(fetch(C), expected)
    @test isapprox(fetch(D), expected)

    # Tree reduce
    As = [rand(1000) for _ in 1:1000]
    expected = reduce((x,y)->x .+ y, As)
    if args_chunks
        As = map(A->remotecall_fetch(Dagger.tochunk, args_loc, A), As)
    elseif args_thunks
        As = map(A->(Dagger.@spawn scope=Dagger.scope(worker=args_loc) copy(A)), As)
    end
    Dagger.spawn_datadeps(;aliasing) do
        to_reduce = Vector[]
        push!(to_reduce, As)
        while !isempty(to_reduce)
            As = pop!(to_reduce)
            n = length(As)
            if n == 2
                Dagger.@spawn Base.mapreducedim!(identity, +, InOut(As[1]), In(As[2]))
            elseif n > 2
                push!(to_reduce, [As[1], As[div(n,2)+1]])
                push!(to_reduce, As[1:div(n,2)])
                push!(to_reduce, As[div(n,2)+1:end])
            end
        end
    end
    @test isapprox(fetch(As[1]), expected)

    # Cholesky
    m, n = 1000, 1000
    nb = 100
    mt, nt = fld(m+nb-1, nb), fld(n+nb-1, nb)
    M_dense = rand(m, n)
    # Make M positive definite
    M_dense = M_dense * M_dense'
    expected = copy(M_dense); LAPACK.potrf!('L', expected)
    M = [M_dense[i:(i+nb-1), j:(j+nb-1)] for i in 1:nb:m, j in 1:nb:n]
    if args_chunks
        M = map(m->remotecall_fetch(Dagger.tochunk, args_loc, m), M)
    elseif args_thunks
        M = map(m->(Dagger.@spawn scope=Dagger.scope(worker=args_loc) copy(m)), M)
    end
    Dagger.spawn_datadeps(;aliasing) do
        for k in range(1, mt)
            Dagger.@spawn LAPACK.potrf!('L', InOut(M[k, k]))
            for _m in range(k+1, mt)
                Dagger.@spawn BLAS.trsm!('R', 'L', 'T', 'N', 1.0, In(M[k, k]), InOut(M[_m, k]))
            end
            for _n in range(k+1, nt)
                Dagger.@spawn BLAS.syrk!('L', 'N', -1.0, In(M[_n, k]), 1.0, InOut(M[_n, _n]))
                for _m in range(_n+1, mt)
                    Dagger.@spawn BLAS.gemm!('N', 'T', -1.0, In(M[_m, k]), In(M[_n, k]), 1.0, InOut(M[_m, _n]))
                end
            end
        end
    end
    for i in 1:nb:m, j in 1:nb:n
        M_dense[i:(i+nb-1), j:(j+nb-1)] .= fetch(M[div(i,nb)+1, div(j,nb)+1])
    end
    @test isapprox(M_dense, expected)
end

@testset "$(aliasing ? "With" : "Without") Aliasing Support" for aliasing in (true, false)
    @testset "$args_mode Data" for args_mode in (:Raw, :Chunk, :Thunk)
        args_chunks = args_mode == :Chunk
        args_thunks = args_mode == :Thunk
        for nw in (1, 2)
            args_loc = nw == 2 ? 2 : 1
            for nt in (1, 2)
                if nprocs() >= nw && Threads.nthreads() >= nt
                    @testset "$nw Workers, $nt Threads" begin
                        Dagger.with_options(;scope=Dagger.scope(workers=1:nw, threads=1:nt)) do
                            test_datadeps(;args_chunks, args_thunks, args_loc, aliasing)
                        end
                    end
                end
            end
        end
    end
end
